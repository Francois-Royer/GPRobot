buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:5.0.0"
    }
}

apply plugin: 'java'
apply plugin: 'java-library-distribution'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.8
targetCompatibility = 1.8

def robotcode_home = org.gradle.internal.os.OperatingSystem.current().isWindows() ? 'c:/robocode' : "$System.env.HOME/robocode"

repositories {
    mavenCentral()
    maven {
        url "http://maven.icm.edu.pl/artifactory/repo/"
    }
}

dependencies {
    compile files("$robotcode_home/libs/robocode.jar")
    compile 'de.ruedigermoeller:fst:2.56'
}

jar {
    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.MF'
    manifest {
        attributes 'Main-Class': 'gprobot.RunGP',
                'Class-Path': configurations.runtime.files.collect { "lib/$it.name" }.join(' ')
    }
}

shadowJar {
    configurations = [project.configurations.compile]
}

task copyGPBase(type: Copy) {
    from 'src/main/java'
    from 'build/classes/java/main'
    include '**/gpbase/**/*.*'
    include '**/net/sf/javaml/core/**/*.*'
    into "$robotcode_home/robots"
}
