plugins {
    id "java"
    id "java-library-distribution"
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

compileJava {
    sourceCompatibility = '24'
    targetCompatibility = '24'
}

def robotcode_home = org.gradle.internal.os.OperatingSystem.current().isWindows() ? 'c:/robocode' : "$System.env.HOME/robocode"

repositories {
    mavenCentral()
    maven {
        url "https://maven.icm.edu.pl/artifactory/repo/"
    }
}

dependencies {
    implementation 'com.esotericsoftware:kryo:5.3.0'
    implementation 'net.sf.robocode:robocode.api:1.10.0'
    shadow 'com.esotericsoftware:kryo:5.3.0'
}

project.configurations.implementation.setCanBeResolved(true)

tasks.withType(JavaCompile).configureEach {
    options.deprecation = false
}

jar {
    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.MF'
    manifest {
        attributes 'Main-Class': 'gprobot.RunGP',
                'Class-Path': configurations.implementation.collect { "lib/$it.name" }.join(' ')
    }
    dependsOn ('dependencies')
}

shadowJar {
    manifest {
        inheritFrom project.tasks.jar.manifest
    }
    configurations = [project.configurations.shadow]
    dependsOn ('jar')
}

tasks.register('copyBase', Copy) {
    from 'src/main/java'
    from 'build/classes/java/main'
    include '**/tankbase/**/*.*'
    include '**/net/sf/javaml/core/**/*.*'
    into "$robotcode_home/robots"
    dependsOn(':compileJava')
}

tasks.register('copyLib', Copy) {
    from 'build/libs'
    include 'gprobot-all.jar'
    into "$robotcode_home/libs"
    dependsOn('shadowJar', 'copyBase')
}

tasks.register('runGPBase', Exec) {
    workingDir "$robotcode_home"
    environment J2D_D3D: false
    commandLine 'java', '-Xmx512M', '-cp', 'libs/*', '-XX:+IgnoreUnrecognizedVMOptions', '--add-opens=java.base/sun.net.www.protocol.jar=ALL-UNNAMED', '--add-opens=java.base/java.lang.reflect=ALL-UNNAMED', '--add-opens=java.desktop/javax.swing.text=ALL-UNNAMED', '--add-opens=java.desktop/sun.awt=ALL-UNNAMED', 'robocode.Robocode', '-battle', 'battles/GPBaseSample.battle'
    dependsOn 'copyBase'
}

tasks.register('runGPBaseDiamond', Exec) {
    workingDir "$robotcode_home"
    environment J2D_D3D: false
    commandLine 'java', '-Xmx512M', '-cp', 'libs/*', '-XX:+IgnoreUnrecognizedVMOptions', '--add-opens=java.base/sun.net.www.protocol.jar=ALL-UNNAMED', '--add-opens=java.base/java.lang.reflect=ALL-UNNAMED', '--add-opens=java.desktop/javax.swing.text=ALL-UNNAMED', '--add-opens=java.desktop/sun.awt=ALL-UNNAMED', 'robocode.Robocode', '-battle', 'battles/GPBaseDiamond.battle'
    dependsOn 'copyBase'
}

tasks.register('runGP', Exec) {
    commandLine 'java', '-cp', "$robotcode_home/libs/*", 'gprobot.RunGP'
    dependsOn 'copyLib'
}
